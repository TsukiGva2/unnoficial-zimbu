#
# Simplistic picture editor using ZUT (Zimbu Templates).
#
# Main program: Creates an HTTP server and adds servlets to it.
#
# Copyright 2016 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT.ZUT PicEditPage.zut

IMPORT PageArgs.zu
IMPORT ImageServlet.zu

ARG.Int port = NEW("p", "port", 7788, "Port for the HTTP server")


FUNC Main() int
  HTTP.Server server = NEW(port.value())
  # server.setVerbosity(HTTP.Verbosity.high)

  # Redirect / to /index.html.
  string indexPath = "index.html"
  server.redirect("/", "/\(indexPath)")

  # Create the demo page and add the body template.
  IO.print("cwd: \(IO.getdir())")
  PageArgs pageArgs = NEW()
    .setImageName("demo.jpg")
    .setImageDir(IO.getdir())
  ZUT.Page page = NEW("Picture Editor")
  callback<ZUT.HtmlFunc, PageArgs> cb = NEW(PicEditPage.body, pageArgs)
  page.setBody(cb)
  page.addPath("/\(indexPath)")
  server.addPage(page)

  # Handle requests for the image.
  server.addServlet(ImageServlet.NEW())

  # Start serving.
  server.start()

  # Open the page in the default browser.
  string url = "http://localhost:\(port.value())/\(indexPath)"
  IO.print("Opening " .. url)
  server.openInBrowser(indexPath)

  # Exit when the application stops.
  server.waitForExit()

  RETURN 0
}
