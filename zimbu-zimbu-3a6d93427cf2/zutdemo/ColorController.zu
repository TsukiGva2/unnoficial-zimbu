#
# ZUT (Zimbu Templates) demo: color controller
#
# Copyright 2015 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

CLASS ColorController

  FUNC $up(ZUT.Event event) bool
    $add(event, 10)
    RETURN FALSE  # Do not bubble up.
  }

  FUNC $down(ZUT.Event event) bool
    $add(event, -10)
    RETURN FALSE  # Do not bubble up.
  }

  PROC $add(ZUT.Event event, int add)
    ZUT.Element rel = ZUT.getElementById("redvalue")
    ZUT.Element gel = ZUT.getElementById("greenvalue")
    ZUT.Element bel = ZUT.getElementById("bluevalue")
    int rval = rel.getText().toInt()
    int gval = gel.getText().toInt()
    int bval = bel.getText().toInt()
    SWITCH event.element.getAttribute("data-color")
      CASE "red"; rval = $adjust(rval, add); rel.setText(rval.ToString())
      CASE "green"; gval = $adjust(gval, add); gel.setText(gval.ToString())
      CASE "blue"; bval = $adjust(bval, add); bel.setText(bval.ToString())
    }
    ZUT.getElementById("changeme").setBackgroundColor(rval, gval, bval)
  }
  
  FUNC $adjust(int val, int add) int
    int v = val + add
    RETURN v < 0 ? 0 : v > 255 ? 255 : v
  }

}
