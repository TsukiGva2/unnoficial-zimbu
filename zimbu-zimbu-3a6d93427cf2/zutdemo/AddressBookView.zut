#
# ZUT (Zimbu Templates) demo page: address book view
#
# Copyright 2015 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT.PROTO addressbook.proto

MODULE AddressBookView

  CSS
    .booktable {
      margin-top: 10px;
      border: solid 2px #555;
    }
    .headerrow {
      background: #ddf;  # This is a comment.
    }
    .oddrow {
      background: #ddd;
    }
    .evenrow {
      background: #bbb;
    }
    .booktable td {
      padding: 5px 10px;
      border: solid 1px #555;
    }
  }

  TEMPLATE show(Tutorial.AddressBook book) ZUT.Html
    [bool odd]
    <table class="booktable" cellpadding=0 cellspacing=0>
      <tr class="headerrow">
        <td>
          Person
        </td>
        <td>
          ID
        </td>
        <td>
          Email
        </td>
        <td>
          Phone
        </td>
      </tr>
      [FOR person IN book.getPersonList()]
        <tr class="[=odd ? "oddrow" : "evenrow"]">
          <td>
            [=person.getName()]
          </td>
          <td>
            [=person.getId()]
          </td>
          <td>
            [IF person.hasEmail()]
              [=person.getEmail()]
            [}]
          </td>
          <td>
            [FOR phone IN person.getPhoneList()]
              <div>
                [=phone.getNumber()]
                [IF phone.hasType()]
                  &nbsp;
                  [=phone.getType().ToString().slice(1)]
                [}]
              </div>
            [}]
          </td>
        </tr>
        [odd = !odd]
      [}]
    </table>
  }
}
