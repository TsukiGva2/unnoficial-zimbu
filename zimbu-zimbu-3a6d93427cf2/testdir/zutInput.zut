#
# ZUT (Zimbu Templates) test page
#
# Copyright 2015 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

# These should be passed with "../" prepended.
IMPORT.PROTO ../zutdemo/addressbook.proto
IMPORT Util.zu

MODULE AddressBookView @public

  CSS
    .booktable {
      margin-top: 10px;
      border: solid 2px #555;
    }
    .oddrow {
      background: #ddd;
    }
    .evenrow {
      background: #bbb;
    }
  }

  TEMPLATE show(Tutorial.AddressBook book) ZUT.Html @public
    [bool odd]
    [ZUT.addCss("some" .. "thing", moreCss())]
    <table class="booktable" cellpadding=0 cellspacing=0>
      <tr class="headerrow">
        <td>
          Person
        </td>
        <td>
          ID
        </td>
        <td>
          Email
        </td>
        <td>
          Phone
        </td>
      </tr>
      [FOR person IN book.getPersonList()]
        <tr class="[=odd ? "oddrow" : "evenrow"]">
          <td>
            [=person.getName()]
          </td>
          <td>
            [=person.getId()]
          </td>
          <td>
            [IF person.hasEmail()]
              [=person.getEmail()]
            [}]
          </td>
          <td>
            [FOR phone IN person.getPhoneList()]
              <div>
                [=phone.getNumber()]
                [IF phone.hasType()]
                  &nbsp;
                  [=phone.getType().ToString().slice(1)]
                [}]
              </div>
            [}]
          </td>
        </tr>
        [odd = !odd]
      [}]
    </table>
  }

 TEMPLATE message(bool useDiv) ZUT.Html
    [IF useDiv]
      <div>Hello</div>
    [ELSE]
      <span>Hello</span>
    [}]
  }

  TEMPLATE classes(int nr) ZUT.Attributes @public
    class="something"
  }

  TEMPLATE someText(string tagName) string @public
    This <[=tagName]> will not be escaped here.
  }

  TEMPLATE moreCss() ZUT.Css @public
    .header {
      background: #ddd;
    }
  }

  TEMPLATE getUrl() ZUT.Url @public
    http://www.zimbu.org
  }
}
