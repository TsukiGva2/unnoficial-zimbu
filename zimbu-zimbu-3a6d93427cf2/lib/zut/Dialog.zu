#
# The Zimbu compiler written in Zimbu
#
# Part of the builtin module ZUT: Dialogs
#
# Copyright 2015 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0

IMPORT.ZUT DialogTemplate.zut

#! Each item from this module gets an ALIAS in ZUT.
MODULE Dialog @public

  #= A dialog with text and at least one button.
  CLASS TextDialog IMPLEMENTS ZUT.I_Listener @public
    string      $text #!
    int         $width = 300 #!
    bool        $hasOkButton = TRUE #!
    bool        $hasCancelButton #!
    bool        $displayed #!
    ZUT.Element $el #!
    bool        $cancelled #!
    proc<>      $callback #!

    #= Create a dialog with |text| and at least one button.
    #- Optionally |callback| specifies a method to be invoked when the dialog
    #- is closed.
    #- By default there is an "OK" button.
    NEW(string text, proc<> callback = NIL) @public
      $text = text
      $callback = callback
    }

    #= Displays the dialog, if not already displayed.
    #- This greys-out the whole page, only the dialog can be interacted with.
    PROC $show() @public
      GENERATE_IF Z.lang == "JS"
        IF !$displayed
          $displayed = TRUE
          $el = ZUT.appendToDom(DialogTemplate.textDialog($text, $width,
                                               $hasOkButton, $hasCancelButton))
          $el.addListener(THIS)
        }
      }
    }

    #! For I_Listener, invoked by the DialogController.
    # The argument is TRUE for cancel.
    PROC $trigger(dyn info)
      $cancelled = info
      $close()
    }

    #= Close the dialog, if currently displayed.
    PROC $close() @public
      GENERATE_IF Z.lang == "JS"
        IF $displayed
          $displayed = FALSE
          $el.removeFromDom()
          IF $callback != NIL
            $callback()
          }
        }
      }
    }

    #= Set whether the Cancel button will be displayed.
    PROC $showCancelButton(bool show) @public
      $hasCancelButton = show
    }

    #= Set whether the OK button will be displayed.
    PROC $showOkButton(bool show) @public
      $hasOkButton = show
    }

    #= Set the width of the dialog box in pixels.
    #- The default width is 300 pixels.
    PROC $setWidth(int width) @public
      $width = width
    }

    #= Set the method to be called when the dialog is closed.
    PROC $setCallback(proc<> callback) @public
      $callback = callback
    }

    #= Return wether the Cancel button was clicked.
    #- Only valid after the dialog was closed.
    FUNC $getCancelled() bool @public
      RETURN $cancelled
    }

  }
}
