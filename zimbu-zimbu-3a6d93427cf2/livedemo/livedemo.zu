# Live demo for Zimbu: type code and immediately see the results
# Main server part of the code.

# Include the UI, this will be produced as HTML and Javascript to be loaded by
# a browser.
IMPORT.ZUT LiveDemoPage.zut

# The servlet that handles build requests from the UI.
IMPORT BuildZimbuServlet.zu

ARG.Int port = NEW("p", "port", 8889, "Port for the HTTP server")


# The main function sets up the HTTP server.
# It runs until killed.
FUNC Main() int
  string indexPath = "index.html"
  HTTP.Server server = NEW(port.value())

  # Create the demo page and add the body template.
  ZUT.Page page = NEW("Zimbu live demo")
  callback<ZUT.HtmlFunc> cb = NEW(LiveDemoPage.demoPage)
  page.setBody(cb)
  page.addPath("/\(indexPath)")
  server.addPage(page)

  # Redirect / to /index.html.
  server.redirect("/", "/" .. indexPath)

  # Handle build and run RPC requests.
  server.addServlet(BuildZimbuServlet.NEW("dummy"))

  # Run the HTTP server in a separate thread.
  server.start()

  # Open the page in a browser.
  string url = "http://localhost:" .. port.value() .. "/" .. indexPath
  IO.print("Opening " .. url)
  server.openInBrowser(indexPath)

  # Run until the application stops.
  server.waitForExit()

  RETURN 0
}
