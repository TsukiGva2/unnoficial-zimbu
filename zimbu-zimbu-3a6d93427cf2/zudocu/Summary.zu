#
# Part of Zudocu.
#
# Copyright 2015 Bram Moolenaar  All Rights Reserved.
# Licensed under the Apache License, Version 2.0.  See the LICENSE file or
# obtain a copy at: http://www.apache.org/licenses/LICENSE-2.0
#

IMPORT.ZUT DocPage.zut

CLASS Summary @items=public

  CLASS SummaryEntry
    string $itemName   # name of the item that this entry is for
                       # includes argument types: NEW(Foo,IO.File)
    ZUT.Html $tableRow   # the html to put in the HTML file; NIL for hidden
                         # entry

    NEW(string name, ZUT.Html row)
      $itemName = name
      $tableRow = row
    }
  }

  list<SummaryEntry> $entries
  int                $count  # number of not hidden, item entries
  bool               $addedSeparator

  NEW()
    $entries = NEW()
  }

  FUNC $Size() int
    RETURN $count
  }
  
  # Add an entry. "row" can be NIL for a hidden entry.
  PROC $add(string itemName, ZUT.Html row)
    $entries.add(NEW(itemName, row))
    IF itemName != "" && row != NIL
      ++$count
    }
    $addedSeparator = FALSE
  }

  PROC $addSep()
    # Only add a separator if there is a non-separator item at the end of the
    # list.
    IF $entries.Size() > 0 && !$addedSeparator
      $addedSeparator = TRUE
      $entries.add(NEW("", DocPage.tableSep()))
    }
  }

  PROC $addHeader(ZUT.Html html)
    ZUT.Html row = DocPage.tableRow3(html)
    $entries.add(NEW("", row))
    $addedSeparator = FALSE
  }

  # Return all the entry rows concatenated.
  # Skip items already in |done|, add listed items to |done|.
  FUNC $concatenate(set<string> done) ZUT.Html
    ZUT.Html ret = NEW()
    FOR e IN $entries
      IF e.itemName == "" || !done.has(e.itemName)
        IF e.itemName != ""
          done.add(e.itemName)
        }
        IF e.tableRow != NIL
          ret.add(e.tableRow)
        }
      }
    }
    RETURN ret
  }

}
